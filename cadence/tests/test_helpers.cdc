import Test
import "FlowCreditMarket"

/* --- Global test constants --- */

access(all) let defaultTokenIdentifier = "A.0000000000000007.MOET.Vault"
access(all) let defaultUFixVariance = 0.00000001
// Variance for UFix64 comparisons
access(all) let defaultUIntVariance: UInt128 = 1_000_000_000_000_000
// Variance for UFix128 comparisons
access(all) let defaultUFix128Variance: UFix128 = 0.00000001 as UFix128

// Health values
access(all) let minHealth = 1.1
access(all) let targetHealth = 1.3
access(all) let maxHealth = 1.5
// UFix128 equivalents (kept same variable names for minimal test churn)
access(all) var intMinHealth: UFix128 = 1.1 as UFix128
access(all) var intTargetHealth: UFix128 = 1.3 as UFix128
access(all) var intMaxHealth: UFix128 = 1.5 as UFix128
access(all) let ceilingHealth: UFix128 = UFix128.max      // infinite health when debt ~ 0.0

/* --- Test execution helpers --- */

access(all)
fun _executeScript(_ path: String, _ args: [AnyStruct]): Test.ScriptResult {
    return Test.executeScript(Test.readFile(path), args)
}

access(all)
fun _executeTransaction(_ path: String, _ args: [AnyStruct], _ signer: Test.TestAccount): Test.TransactionResult {
    let txn = Test.Transaction(
        code: Test.readFile(path),
        authorizers: [signer.address],
        signers: [signer],
        arguments: args
    )    
    return Test.executeTransaction(txn)
}

access(all)
fun grantBeta(_ admin: Test.TestAccount, _ grantee: Test.TestAccount): Test.TransactionResult {
    let signers = admin.address == grantee.address ? [admin] : [admin, grantee]
    let betaTxn = Test.Transaction(
        code: Test.readFile("./transactions/flow-credit-market/pool-management/03_grant_beta.cdc"),
        authorizers: [admin.address, grantee.address],
        signers: signers,
        arguments: []
    )
    return Test.executeTransaction(betaTxn)
}
/* --- Setup helpers --- */

// Common test setup function that deploys all required contracts
access(all)
fun deployContracts() {
    var err = Test.deployContract(
        name: "DeFiActionsUtils",
        path: "../../FlowActions/cadence/contracts/utils/DeFiActionsUtils.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())
    // Deploy FlowCreditMarketMath before FlowCreditMarket
    err = Test.deployContract(
        name: "FlowCreditMarketMath",
        path: "../lib/FlowCreditMarketMath.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())
    err = Test.deployContract(
        name: "DeFiActions",
        path: "../../FlowActions/cadence/contracts/interfaces/DeFiActions.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())

    let initialSupply = 0.0
    err = Test.deployContract(
        name: "MOET",
        path: "../contracts/MOET.cdc",
        arguments: [initialSupply]
    )
    Test.expect(err, Test.beNil())

    err = Test.deployContract(
        name: "FlowCreditMarket",
        path: "../contracts/FlowCreditMarket.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())

    // NOTE: Do not publish beta capability here; some tests create the Pool later and
    // publishing before pool creation will fail. Tests that need the cap should call
    // grantPoolCapToConsumer() after creating the pool.

    // Deploy MockFlowCreditMarketConsumer
    err = Test.deployContract(
        name: "MockFlowCreditMarketConsumer",
        path: "../contracts/mocks/MockFlowCreditMarketConsumer.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())

    err = Test.deployContract(
        name: "MockOracle",
        path: "../contracts/mocks/MockOracle.cdc",
        arguments: [defaultTokenIdentifier]
    )
    Test.expect(err, Test.beNil())

    let initialYieldTokenSupply = 0.0
    err = Test.deployContract(
        name: "MockYieldToken",
        path: "../contracts/mocks/MockYieldToken.cdc",
        arguments: [initialYieldTokenSupply]
    )
    Test.expect(err, Test.beNil())

    err = Test.deployContract(
        name: "DummyConnectors",
        path: "../contracts/mocks/DummyConnectors.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())

    // Deploy FungibleTokenConnectors
    err = Test.deployContract(
        name: "FungibleTokenConnectors",
        path: "../../FlowActions/cadence/contracts/connectors/FungibleTokenConnectors.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())
    // Deploy MockDexSwapper for DEX liquidation tests
    err = Test.deployContract(
        name: "MockDexSwapper",
        path: "../contracts/mocks/MockDexSwapper.cdc",
        arguments: []
    )
    Test.expect(err, Test.beNil())
}

/* --- Script Helpers --- */

access(all)
fun getBalance(address: Address, vaultPublicPath: PublicPath): UFix64? {
    let res = _executeScript("../scripts/tokens/get_balance.cdc", [address, vaultPublicPath])
    Test.expect(res, Test.beSucceeded())
    return res.returnValue as! UFix64?
}

access(all)
fun getReserveBalance(vaultIdentifier: String): UFix64 {
    let res = _executeScript("../scripts/flow-credit-market/get_reserve_balance_for_type.cdc", [vaultIdentifier])
    Test.expect(res, Test.beSucceeded())
    return res.returnValue as! UFix64
}

access(all)
fun getAvailableBalance(pid: UInt64, vaultIdentifier: String, pullFromTopUpSource: Bool, beFailed: Bool): UFix64 {
    let res = _executeScript("../scripts/flow-credit-market/get_available_balance.cdc",
            [pid, vaultIdentifier, pullFromTopUpSource]
        )
    Test.expect(res, beFailed ? Test.beFailed() : Test.beSucceeded())
    return res.status == Test.ResultStatus.failed ? 0.0 : res.returnValue as! UFix64
}

access(all)
fun getPositionHealth(pid: UInt64, beFailed: Bool): UFix128 {
    let res = _executeScript("../scripts/flow-credit-market/position_health.cdc",
            [pid]
        )
    Test.expect(res, beFailed ? Test.beFailed() : Test.beSucceeded())
    return res.status == Test.ResultStatus.failed ? 0.0 as UFix128 : res.returnValue as! UFix128
}

access(all)
fun getPositionDetails(pid: UInt64, beFailed: Bool): FlowCreditMarket.PositionDetails {
    let res = _executeScript("../scripts/flow-credit-market/position_details.cdc",
            [pid]
        )
    Test.expect(res, beFailed ? Test.beFailed() : Test.beSucceeded())
    return res.returnValue as! FlowCreditMarket.PositionDetails
}

access(all)
fun poolExists(address: Address): Bool {
    let res = _executeScript("../scripts/flow-credit-market/pool_exists.cdc", [address])
    Test.expect(res, Test.beSucceeded())
    return res.returnValue as! Bool
}

access(all)
fun fundsAvailableAboveTargetHealthAfterDepositing(
    pid: UInt64,
    withdrawType: String,
    targetHealth: UFix128,
    depositType: String,
    depositAmount: UFix64,
    beFailed: Bool
): UFix64 {
    let res = _executeScript("../scripts/flow-credit-market/funds_avail_above_target_health_after_deposit.cdc",
            [pid, withdrawType, targetHealth, depositType, depositAmount]
        )
    Test.expect(res, beFailed ? Test.beFailed() : Test.beSucceeded())
    return res.returnValue as! UFix64
}

access(all)
fun fundsRequiredForTargetHealthAfterWithdrawing(
    pid: UInt64,
    depositType: String,
    targetHealth: UFix128,
    withdrawType: String,
    withdrawAmount: UFix64,
    beFailed: Bool
): UFix64 {
    let res = _executeScript("../scripts/flow-credit-market/funds_req_for_target_health_after_withdraw.cdc",
            [pid, depositType, targetHealth, withdrawType, withdrawAmount]
        )
    Test.expect(res, beFailed ? Test.beFailed() : Test.beSucceeded())
    return res.returnValue as! UFix64
}

/* --- Transaction Helpers --- */

access(all)
fun createAndStorePool(signer: Test.TestAccount, defaultTokenIdentifier: String, beFailed: Bool) {
    let createRes = _executeTransaction(
        "../transactions/flow-credit-market/pool-factory/create_and_store_pool.cdc",
        [defaultTokenIdentifier],
        signer
    )
    Test.expect(createRes, beFailed ? Test.beFailed() : Test.beSucceeded())

    // Enable debug logs for tests to aid diagnostics
    let debugRes = _executeTransaction(
        "../transactions/flow-credit-market/pool-governance/set_debug_logging.cdc",
        [true],
        signer
    )
    Test.expect(debugRes, Test.beSucceeded())
}

access(all)
fun setMockOraclePrice(signer: Test.TestAccount, forTokenIdentifier: String, price: UFix64) {
    let setRes = _executeTransaction(
        "./transactions/mock-oracle/set_price.cdc",
        [forTokenIdentifier, price],
        signer
    )
    Test.expect(setRes, Test.beSucceeded())
}

access(all)
fun addSupportedTokenSimpleInterestCurve(
    signer: Test.TestAccount,
    tokenTypeIdentifier: String,
    collateralFactor: UFix64,
    borrowFactor: UFix64,
    depositRate: UFix64,
    depositCapacityCap: UFix64
) {
    let additionRes = _executeTransaction(
        "../transactions/flow-credit-market/pool-governance/add_supported_token_simple_interest_curve.cdc",
        [ tokenTypeIdentifier, collateralFactor, borrowFactor, depositRate, depositCapacityCap ],
        signer
    )
    Test.expect(additionRes, Test.beSucceeded())
}

access(all)
fun addSupportedTokenSimpleInterestCurveWithResult(
    signer: Test.TestAccount,
    tokenTypeIdentifier: String,
    collateralFactor: UFix64,
    borrowFactor: UFix64,
    depositRate: UFix64,
    depositCapacityCap: UFix64
): Test.TransactionResult {
    return _executeTransaction(
        "../transactions/flow-credit-market/pool-governance/add_supported_token_simple_interest_curve.cdc",
        [ tokenTypeIdentifier, collateralFactor, borrowFactor, depositRate, depositCapacityCap ],
        signer
    )
}

access(all)
fun rebalancePosition(signer: Test.TestAccount, pid: UInt64, force: Bool, beFailed: Bool) {
    let rebalanceRes = _executeTransaction(
        "../transactions/flow-credit-market/pool-management/rebalance_position.cdc",
        [ pid, force ],
        signer
    )
    Test.expect(rebalanceRes, beFailed ? Test.beFailed() : Test.beSucceeded())
}

access(all)
fun setupMoetVault(_ signer: Test.TestAccount, beFailed: Bool) {
    let setupRes = _executeTransaction("../transactions/moet/setup_vault.cdc", [], signer)
    Test.expect(setupRes, beFailed ? Test.beFailed() : Test.beSucceeded())
}

access(all)
fun mintMoet(signer: Test.TestAccount, to: Address, amount: UFix64, beFailed: Bool) {
    let mintRes = _executeTransaction("../transactions/moet/mint_moet.cdc", [to, amount], signer)
    Test.expect(mintRes, beFailed ? Test.beFailed() : Test.beSucceeded())
}


// Transfer Flow tokens from service account to recipient
access(all)
fun transferFlowTokens(to: Test.TestAccount, amount: UFix64) {
    let transferTx = Test.Transaction(
        code: Test.readFile("../transactions/flowtoken/transfer_flowtoken.cdc"),
        authorizers: [Test.serviceAccount().address],
        signers: [Test.serviceAccount()],
        arguments: [to.address, amount]
    )
    let res = Test.executeTransaction(transferTx)
    Test.expect(res, Test.beSucceeded())
}


access(all)
fun expectEvents(eventType: Type, expectedCount: Int) {
    let events = Test.eventsOfType(eventType)
    Test.assertEqual(expectedCount, events.length)
}

access(all)
fun withdrawReserve(
    signer: Test.TestAccount,
    poolAddress: Address,
    tokenTypeIdentifier: String,
    amount: UFix64,
    recipient: Address,
    beFailed: Bool
) {
    let txRes = _executeTransaction(
        "../transactions/flow-credit-market/pool-governance/withdraw_reserve.cdc",
        [poolAddress, tokenTypeIdentifier, amount, recipient],
        signer
    )
    Test.expect(txRes, beFailed ? Test.beFailed() : Test.beSucceeded())
}

/* --- Capability Helpers --- */

// Grants the Pool capability with EParticipant and EPosition entitlements to the MockFlowCreditMarketConsumer account (0x8)
// Must be called AFTER the pool is created and stored, otherwise publishing will fail the capability check.
access(all)
fun grantPoolCapToConsumer() {
    let protocolAccount = Test.getAccount(0x0000000000000007)
    let consumerAccount = Test.getAccount(0x0000000000000008)
    // Check pool exists (defensively handle CI ordering). If not, no-op.
    let existsRes = _executeScript("../scripts/flow-credit-market/pool_exists.cdc", [protocolAccount.address])
    Test.expect(existsRes, Test.beSucceeded())
    if !(existsRes.returnValue as! Bool) {
        return
    }

    // Use in-repo grant transaction that issues EParticipant+EPosition and saves to PoolCapStoragePath
    let grantRes = grantBeta(protocolAccount, consumerAccount)
    Test.expect(grantRes, Test.beSucceeded())
}
/* --- Assertion Helpers --- */

access(all) fun equalWithinVariance(_ expected: AnyStruct, _ actual: AnyStruct): Bool {
    let expectedType = expected.getType()
    let actualType = actual.getType()
    if expectedType == Type<UFix64>() && actualType == Type<UFix64>() {
        return ufixEqualWithinVariance(expected as! UFix64, actual as! UFix64)
    } else if expectedType == Type<UFix128>() && actualType == Type<UFix128>() {
        return ufix128EqualWithinVariance(expected as! UFix128, actual as! UFix128)
    }
    panic("Expected and actual types do not match - expected: \(expectedType.identifier), actual: \(actualType.identifier)")
}

access(all) fun ufixEqualWithinVariance(_ expected: UFix64, _ actual: UFix64): Bool {
    // return true if expected is within defaultUFixVariance of actual, false otherwise and protect for underflow`
    let diff = Fix64(expected) - Fix64(actual)
    // take the absolute value of the difference without relying on .abs()
    let absDiff: UFix64 = diff < 0.0 ? UFix64(-1.0 * diff) : UFix64(diff)
    return absDiff <= defaultUFixVariance
}

access(all) fun ufix128EqualWithinVariance(_ expected: UFix128, _ actual: UFix128): Bool {
    let absDiff: UFix128 = expected >= actual ? expected - actual : actual - expected
    return absDiff <= defaultUFix128Variance
}
